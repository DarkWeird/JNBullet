plugins {
    id "org.standardout.versioneye" version "1.0.1"
}


/*******************************************************************************
 * Copyright 2011 See AUTHORS file.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

apply plugin: 'java'
//apply plugin: 'project-report'
//apply plugin: 'idea'
//apply plugin: 'eclipse'
apply plugin: 'maven'
//apply plugin: 'checkstyle'
//apply plugin: 'pmd'
//apply plugin: 'findbugs'


sourceCompatibility = 1.7
version = '1.0'

// gradle wrapper version
//wrapper {
//    gradleVersion '2.6'
//}

ext {
    generatedSrcDir = "jni/swig-src/collision:jni/swig-src/dynamics:jni/swig-src/extras:jni/swig-src/linearmath:jni/swig-src/softbody:src"

    // Read environment variables, including variables passed by jenkins continuous integration server
    env = System.getenv()
}


repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven {url "http://artifactory.terasology.org/artifactory/virtual-repo-live"}
}

sourceSets {
    main {
        java {
            srcDir generatedSrcDir
        }
    }
}

ant.importBuild('jni/swig/build.xml') { antTaskName ->
    "swig_${antTaskName}".toString()
}

// import and rename ant tasks
ant.importBuild('jni/build.xml') { antTaskName ->
    "jni_${antTaskName}".toString()
}

// regroup ant tasks
tasks.matching { task ->
    task.name.startsWith('swig_') or task.name.startsWith('jni_')
}*.group = 'Ant'


//assemble.dependsOn('jni_pack_natives')
jni_pack_natives.dependsOn('jni_compile_natives')
assemble.dependsOn swig_all


// Library and distribution config
group = 'org.terasology'
description = 'A Physics library'


task sourceJar(type: Jar, dependsOn: jni_pack_natives) {
    description = "Create a JAR with all sources"
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = "Create a JAR with the JavaDoc for the java sources"
    from javadoc.destinationDir
    classifier = 'javadoc'
}

// Define the artifacts we want to publish (the .pom will also be included since the Maven plugin is active)
artifacts {
    archives sourceJar
    archives javadocJar
}




dependencies {
    compile group: 'org.terasology', name: 'TeraMath', version: '1.4.0'
    compile "com.badlogicgames.gdx:gdx-jnigen:1.9.7-SNAPSHOT"
    compile "com.badlogicgames.gdx:gdx:1.9.7-SNAPSHOT"

}


