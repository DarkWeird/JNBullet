apply plugin: 'base'
apply plugin: 'maven-publish'

version = '0.1.0-SNAPSHOT'
group = 'org.terasology.terabullet_2'

task zipNatives(type: Zip) {
    description 'Creates a zip archive that contains all TeraBullet native files'

    from ('linux_amd64_gcc') {
        include '*Linux*'
        into 'linux'
    }
    
    from ('linux_i386_gcc') {
        include '*Linux*'
        into 'linux'
    }

    from ('linux_windows_amd64_mingw32') {
        include '*Windows*'
        into 'windows'
    }
    
    from ('linux_windows_i686_mingw32') {
        include '*Windows*'
        into 'windows'
    }

  //  from ('natives') {
   //     include '*Darwin*'
   //     into 'macosx'
   // }

    destinationDir = file(buildDir)
    baseName = 'terabullet_natives'
}

publishing {
    publications {
        zippedNatives(MavenPublication) {
            artifact zipNatives

            artifactId 'terabullet_natives'
        }
    }

    repositories {
        maven {
            // First see if we have properties with creds (gradle.properties)
            if (project.hasProperty('artifactoryUser') && project.hasProperty('artifactoryPass')) {
                credentials {
                    username artifactoryUser
                    password artifactoryPass
                }
            }
            url "http://artifactory.terasology.org/artifactory/libs-snapshot-local"
        }
    }
}
